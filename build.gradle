plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.14.2'
  id 'org.springframework.boot' version '2.7.4'
  id "org.sonarqube" version "3.4.0.2513"
  id 'com.google.cloud.tools.jib' version '3.3.0'
}

//apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

scmVersion {
  tag {
    prefix = ''
  }
}

group = 'de.gessnerfl.rabbitmq'
description = """RabbitMQ :: Queue Management :: List/Move/Delete dead messages from queues"""
project.version = scmVersion.version

ext.scmUrl = 'git@github.com:gessnerfl/rabbitmq-queue-management.git'
ext.projectUrl = 'https://github.com/gessnerfl/rabbitmq-queue-management'

sourceCompatibility = 17
targetCompatibility = 17

repositories { mavenCentral() }

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.security:spring-security-ldap'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
  implementation 'org.springframework.boot:spring-boot-devtools'
  implementation('org.springframework.boot:spring-boot-starter-validation')

  implementation 'com.nimbusds:nimbus-jose-jwt:9.25.4'

  implementation 'io.github.openfeign:feign-core:11.10'
  implementation 'io.github.openfeign:feign-gson:11.10'
  implementation 'io.github.openfeign:feign-slf4j:11.10'

  implementation 'com.google.code.gson:gson:2.9.1'
  implementation 'commons-codec:commons-codec:1.15'

  implementation 'org.webjars.bowergithub.caldwell:renderjson:1.4.0'
  implementation 'org.webjars.bower:bootstrap:5.2.0'
  implementation 'org.webjars.bower:open-iconic:1.1.1'

  implementation 'com.rabbitmq:amqp-client:5.16.0'

  testImplementation 'com.unboundid:unboundid-ldapsdk:6.0.6'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'org.hamcrest:hamcrest-core:2.2'
  testImplementation 'org.awaitility:awaitility:4.2.0'
  testImplementation 'org.testcontainers:testcontainers:1.17.4'
  testImplementation 'org.testcontainers:junit-jupiter:1.17.4'
  testImplementation 'org.apache.commons:commons-lang3:3.12.0'
}

java {
  withSourcesJar()
}

bootJar {
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.PropertiesLauncher'
    }
}

test {
  finalizedBy jacocoTestReport
  useJUnitPlatform()
}

jacocoTestReport {
  dependsOn test
  reports {
    xml.required = true
    csv.required = false
    html.required = true
  }
}

sonarqube {
  properties {
    property 'sonar.host.url', 'https://sonarcloud.io'
    property 'sonar.organization', 'gessnerfl-github'
    property "sonar.projectName", "RabbitMQ Queue Managment"
    property "sonar.projectKey", "de.gessnerfl.rabbitmq-queue-management"
  }
}

jib{
  from {
    image = 'amazoncorretto:17.0.4-al2'
    container {
      ports = ["8780", "8781"]
    }/*
    platforms {
      platform {
        architecture = 'amd64'
        os = 'linux'
      }
      platform {
        architecture = 'arm64'
        os = 'linux'
      }
    }*/
  }
  to {
    image = 'gessnerfl/rabbitmq-queue-management'
    tags = [scmVersion.version, 'latest']
  }
}